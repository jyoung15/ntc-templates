Value Filldown ACL_NAME (.+?)
Value Key,Required INDEX (\d+)
Value DIRECTION (In|Out|Any)
Value Required SOURCE ([\da-f:/]+)
Value Required DESTINATION ([\da-f:/]+)
Value Required PROTOCOL (\d+|Any)
Value Required SOURCE_PORT (\d+-\d+)
Value Required DESTINATION_PORT (\d+-\d+)
Value Required DSCP (\S+)
Value Required ACTION (Permit|Deny)
Value COUNTER (\d+)


Start
  ^.*\bshow(\s+flexconnect)?\s+ipv6\s+acl\s+detailed\s+${ACL_NAME}\s*$$
  ^Rule Index\s*\.+ ${INDEX} -> Rule
  ^Deny Counter -> EOF

Rule
  ^\s*$$ -> Record Start
  ^Direction\s*\.+ ${DIRECTION}
  ^IPv6 source prefix\s*\.+ ${SOURCE}
  ^IPv6 destination prefix\s*\.+ ${DESTINATION}
  ^Protocol\s*\.+ ${PROTOCOL}
  ^Source Port Range\s*\.+ ${SOURCE_PORT}
  ^Destination Port Range\s*\.+ ${DESTINATION_PORT}
  ^DSCP\s*\.+ ${DSCP}
  ^Action\s*\.+ ${ACTION}
  ^Counter\s*\.+ ${COUNTER}
  ^.* -> Error
