Value TIME (.+?)
Value TZ_DELTA (\S+)
Value TIMEZONE (.+)
Value TZ_NAME (.+?)
Value NTP_VERSION ([34])
Value NTP_POLLING_INTERVAL (\d+)
Value List INDEX (\d+)
Value List NTP_KEY_INDEX (\d+)
Value List NTP_SERVER (\S+)
Value List STATUS (\S+\s?\S+)
Value List NTP_MSG_AUTH_STATUS (\S+\s?\S+)
Value List NTP_V4_KEY_TYPE (\S+)
Value List NTP_V4_MAX_POLL (\d+)
Value List NTP_V4_MIN_POLL (\d+)


Start
  ^Time\s*\.+\s+${TIME}\s*$$
  ^Timezone [dD]elta\s*\.+\s+${TZ_DELTA}\s*$$
  ^Timezone [lL]ocation\s*\.+\s+\(${TIMEZONE}\)\s+${TZ_NAME}\s*$$
  ^NTP [sS]ervers\s*$$
  ^\s*NTP [vV]ersion\s*\.+\s+${NTP_VERSION}\s*$$ -> Continue
  ^\s*NTP [vV]ersion\s*\.+\s+3 -> NTPv3
  ^\s*NTP [vV]ersion\s*\.+\s+4 -> NTPv4
  ^\s*NTP [pP]olling [iI]nterval\s*\.+\s+${NTP_POLLING_INTERVAL}\s*$$ -> NTPv3

NTPv3
  ^\s*NTP [pP]olling [iI]nterval\s*\.+\s+${NTP_POLLING_INTERVAL}\s*$$
  #
  # NTP Server Table
  ^\s*Index\s+NTP Key Index\s+NTP Server\s+Status\s+NTP Msg Auth Status\s*$$ -> NTPv3_Servers
  ^\s*Index\s+NTP Key Index\s+NTP Server\s+NTP Msg Auth Status\s*$$ -> NTPv3_Servers_Legacy
  ^\s*$$

NTPv3_Servers
  ^\s+${INDEX}\s+${NTP_KEY_INDEX}\s+${NTP_SERVER}\s+${STATUS}\s+${NTP_MSG_AUTH_STATUS}\s*$$
  ^\s*[-\s]+$$
  ^\s*$$ -> Record Start
  ^. -> Error

NTPv3_Servers_Legacy
  ^\s+${INDEX}\s+${NTP_KEY_INDEX}\s+${NTP_SERVER}\s+${NTP_MSG_AUTH_STATUS}\s*$$
  ^\s*[-\s]+$$
  ^\s*$$ -> Record Start
  ^. -> Error

NTPv4
  ^Index\s+NTP Key\s+NTP Server\s+NTP Key\s+Polling Intervals
  ^\s+Index\s+Type\s+Max\s+Min\s*$$ -> NTPv4_Servers
  ^\s*$$

NTPv4_Servers
  ^\s+${INDEX}\s+${NTP_KEY_INDEX}\s+${NTP_SERVER}\s+${NTP_V4_KEY_TYPE}\s+${NTP_V4_MAX_POLL}\s+${NTP_V4_MIN_POLL}\s*$$
  ^\s*[-\s]+$$
  ^\s*$$ -> Record Start
  ^. -> Error
